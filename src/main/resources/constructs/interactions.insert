prefix np: <http://www.nanopub.org/nschema#>
prefix wp: <http://vocabularies.wikipathways.org/wp#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix prov: <http://www.w3.org/ns/prov#>
prefix wd: <https://www.wikidata.org/entity/>
prefix dc: <http://purl.org/dc/elements/1.1/>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?interaction a wp:Interaction ;
      wp:source ?source ;
      wp:target ?target ;
      wp:organismName ?organism .
    ?source a ?sourceType .
    ?target a ?targetType .
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?pathway .
    ?pathway dc:title ?pathwayTitle .
    ?assertionGraph has-source: ?pmidURI .
  }
  graph ?publicationInfoGraph {
    ?nanopub dcterms:created ?now ;
      dcterms:rights <https://creativecommons.org/licenses/by/3.0/> ;
      prov:wasDerivedFrom ?pathway ;
      prov:wasGeneratedBy ?activity .
    ?activity a prov:Activity; 
      prov:used <https://github.com/wikipathways/nanopublications> ;
      prov:atLocation wd:Q1137652 .
  }
} where {
  ?interaction wp:source ?source . ?source a ?sourceType .
  ?interaction wp:target ?target . ?target a ?targetType .
  ?pathway a wp:Pathway ;
    dc:title ?pathwayTitle ;
    wp:organismName ?organism .
  ?interaction a wp:Interaction ;
    dcterms:isPartOf ?pathway ;
    dcterms:bibliographicCitation ?pmidURI .
  FILTER (?sourceType != wp:DataNode)
  FILTER (?targetType != wp:DataNode)
  bind(iri(concat("http://purl.org/np/", md5(str(?interaction)))) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(iri(concat(str(?nanopub),"activity")) as ?activity)
  bind(now() as ?now)
}
